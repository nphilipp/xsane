dnl Process this file with autoconf to produce a configure script. -*-sh-*-
AC_INIT(include/config.h.in)
AC_CONFIG_HEADER(include/config.h)
# AC_PREREQ(2.10)dnl		dnl Minimum Autoconf version required.
# AC_ARG_PROGRAM

# version code:
V_MAJOR=0
V_MINOR=999

PACKAGE=xsane

BINPROGS="xsane"

# languages
ALL_LINGUAS="ca cs da de es fi fr hu it ja nl pa pl pt pt_BR ro ru sk sl sr sv vi tr zh zh_CN"

SANE_V_MAJOR=1
VERSION=${V_MAJOR}.${V_MINOR}
XSANE_PACKAGE_VERSION="$PACKAGE-$VERSION"
AC_DEFINE_UNQUOTED(PACKAGE, "$PACKAGE", [Define to the name of the distribution])
AC_DEFINE_UNQUOTED(VERSION, "$VERSION", [Define to the version of the distribution])
AC_DEFINE_UNQUOTED(XSANE_PACKAGE_VERSION, "$XSANE_PACKAGE_VERSION", [Define to name and version of distribution])
AC_SUBST(PACKAGE)
AC_SUBST(VERSION)
AC_SUBST(XSANE_PACKAGE_VERSION)
AC_SUBST(SANE_MAJOR)


dnl Check args

echo ""
echo "**************************************************************************"
echo ""

dnl Check for gtk-2 
AC_MSG_CHECKING([whether GTK+-2.0 is requested])
AC_ARG_ENABLE(gtk2, [  --disable-gtk2          do not use GTK+-2.0, if available use GTK+-1.2], USE_GTK2=$enableval, USE_GTK2=yes)
AC_MSG_RESULT($USE_GTK2)

dnl Check for gimp plugin support, default is enabled
AC_MSG_CHECKING([whether GIMP plugin is requested])
AC_ARG_ENABLE(gimp, [  --disable-gimp          do not include GIMP plugin mode], USE_GIMP=$enableval, USE_GIMP=yes)
AC_MSG_RESULT($USE_GIMP)

if test "${USE_GIMP}" = "yes"; then
 dnl Check for gimp-2 plugin support, default is enabled
 AC_MSG_CHECKING([whether GIMP-2.0 is requested])
 AC_ARG_ENABLE(gimp2, [  --disable-gimp2         do not use GIMP-2.0, use GIMP-1.x if available], TRY_GIMP_2=$enableval, TRY_GIMP_2=yes)
 AC_MSG_RESULT($TRY_GIMP_2)
fi

dnl Check for jpeg support
AC_MSG_CHECKING([whether JPEG support is requested])
dnl Default is enabled JPEG
AC_ARG_ENABLE(jpeg, [  --disable-jpeg          do not include JPEG support], USE_JPEG=$enableval, USE_JPEG=yes)
AC_MSG_RESULT($USE_JPEG)

dnl Check for png support
AC_MSG_CHECKING([whether PNG support is requested])
dnl Default is enabled PNG
AC_ARG_ENABLE(png, [  --disable-png           do not include PNG support], USE_PNG=$enableval, USE_PNG=yes)
AC_MSG_RESULT($USE_PNG)

dnl Check for tiff support
AC_MSG_CHECKING([whether TIFF support is requested])
dnl Default is enabled TIFF
AC_ARG_ENABLE(tiff, [  --disable-tiff          do not include TIFF support], USE_TIFF=$enableval, USE_TIFF=yes)
AC_MSG_RESULT($USE_TIFF)

dnl Check for lcms support
AC_MSG_CHECKING([whether LCMS (color management) support is requested])
dnl Default is enabled LCMS
AC_ARG_ENABLE(lcms, [  --disable-lcms          do not include LCMS support], USE_LCMS=$enableval, USE_LCMS=yes)
AC_MSG_RESULT($USE_LCMS)

echo ""
echo "**************************************************************************"
echo ""

dnl Checks for programs.
AC_PROG_CC
AC_AIX
AC_MINIX
AC_ISC_POSIX
AM_PROG_CC_STDC
AC_PROG_INSTALL
AC_PROG_MAKE_SET
AC_PROG_CPP
AC_PROG_GCC_TRADITIONAL

INCLUDES="${INCLUDES} -I/usr/local/include"
CPPFLAGS="${CPPFLAGS} -D_GNU_SOURCE"
if test "${ac_cv_prog_gcc}" = "yes"; then
  CFLAGS="${CFLAGS} -Wall"
fi

AC_GCC_MSNATIVE_STRUCT
if test "${ac_cv_header_windows_h}" = "yes"; then
  LDFLAGS="${LDFLAGS} -mwindows"
  XSANE_ICON="xsane-icon.opc"
fi

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(windows.h os2.h fcntl.h unistd.h libc.h sys/time.h sys/types.h zlib.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_C_INLINE
AC_TYPE_SIGNAL
AC_TYPE_SIZE_T
AC_TYPE_PID_T
AC_CHECK_TYPE(ssize_t, long)
AC_CHECK_TYPE(u_char, unsigned char)
AC_CHECK_TYPE(u_int, unsigned int)
AC_CHECK_TYPE(u_long, unsigned long)

dnl Checks for libraries.
AC_CHECK_LIB(m, sqrt)
AC_CHECK_LIB(z, deflateInit_)

if test "${USE_JPEG}" = "yes"; then
  AC_CHECK_LIB(jpeg, jpeg_start_decompress)
fi

# tiff test must stand after test for zlib
if test "${USE_TIFF}" = "yes"; then
  AC_CHECK_LIB(tiff, TIFFOpen)
fi

if test "${USE_LCMS}" = "yes"; then
  AC_CHECK_LIB(lcms, cmsOpenProfileFromFile)
fi

dnl Checks for library functions.
AC_FUNC_ALLOCA
AC_FUNC_MMAP
AC_CHECK_FUNCS(atexit mkdir sigprocmask strdup strndup strftime strstr strsep strtod snprintf usleep strcasecmp strncasecmp lstat)

dnl Check for NLS/gettext
AM_GNU_GETTEXT
AC_LINK_FILES($nls_cv_header_libgt, $nls_cv_header_intl)

if test "$USE_NLS" = yes; then
  if test "$USE_INCLUDED_LIBINTL" = yes; then
    CPPFLAGS="$CPPFLAGS -I\$(top_builddir)/intl"
    INTLSUB=intl
  fi
fi   

AC_SUBST(INTLSUB)

AM_PATH_SANE(1.0.0, HAVE_SANE=yes, )

if test "${USE_GIMP}" = "yes"; then
  if test "${TRY_GIMP_2}" = "yes"; then
    AM_PATH_GIMP_2_0(1.3.23, HAVE_GIMP=yes, )
  fi
fi

if test "${HAVE_GIMP}" = "yes"; then
  HAVE_GTK=yes
  AC_DEFINE([HAVE_GIMP_2], 1, [Define to 1 if you have gimp-2.0.])
else
  dnl *** We do not have gimp-2.0 ***
  dnl *** if xsane is compiled as gimp plugin it is possible that we do not
  dnl *** need the test for gtk, but when we have gimp-1.0.x installed
  dnl *** (gimp-1.0.x does not have gimp-config) or we compile xsane without
  dnl *** gimp support we need the test for gtk.
  dnl *** we start with the test for GTK+-2.0, if this fails or is diabled
  dnl *** then we test for gtk+-1.2

  if test "${USE_GTK2}" = "yes"; then
    AM_PATH_GTK_2_0(2.0.0, HAVE_GTK=yes, )
  fi

  if test "x${HAVE_GTK}" = "x"; then
    AM_PATH_GTK(1.2.0, HAVE_GTK=yes, )
  fi

  if test "${USE_GIMP}" = "yes"; then
   dnl *** now we can test for gimp-1.x.x, for gimp-1.0.x we need GTK_* settings ***
   AM_PATH_GIMP_ORAUCH(1.0.0, HAVE_GIMP=yes)
  fi
fi

if test "${HAVE_GIMP}" = "yes"; then
  dnl *** we have gimp ***
  dnl *** define macro HAVE_ANY_GIMP for sourcecode tests for gimp ***
  AC_DEFINE([HAVE_ANY_GIMP], 1, [Define to 1 if you have gimp - no matter what version.])
fi
 

# png test must stand after test for zlib
# png test must stand behind GTK-check (X-Window-check)
#  because png.h is located in the X11 directory on some systems
#  so we have to use GTK_CFLAGS and GTK_LIBS to test for png
if test "${USE_PNG}" = "yes"; then
  saved_CPPFLAGS="${CPPFLAGS}"

  if test "x${HAVE_GTK}" != "x"; then
    CPPFLAGS="${CPPFLAGS} ${GTK_CFLAGS}"
  fi

  AC_CHECK_HEADERS(png.h, [AC_CHECK_LIB(png, png_create_info_struct,,, ${GTK_LIBS})])
  CPPFLAGS="${saved_CPPFLAGS}"
fi


AC_SUBST(INCLUDES)
AC_SUBST(BINPROGS)
AC_SUBST(GTK_CFLAGS)
AC_SUBST(GTK_LIBS)
AC_SUBST(GIMP_LIBS)
CPPFLAGS="${CPPFLAGS} \
	  -DV_MAJOR=${V_MAJOR} -DV_MINOR=${V_MINOR} -DSANE_V_MAJOR=${SANE_V_MAJOR}"

dnl Print error message if sane or gtk is missing
if test "x${HAVE_SANE}" = "x"; then
  echo "****************************************************************"
  echo "ERROR: SANE-1.0.0 or newer is needed for compiling xsane"
  echo "     - if you installed SANE as rpm make sure you also included"
  echo "       sane-devel"
  echo "****************************************************************"
  echo ""
  exit
fi

if test "x${HAVE_GTK}" = "x"; then
  echo "****************************************************************"
  echo "ERROR: GTK-1.2.0 or newer is needed for compiling xsane"
  echo "       if you installed gtk as rpm make sure you also included"
  echo "       gtk-devel"
  echo "****************************************************************"
  echo ""
  exit
fi


AC_SUBST(V_MAJOR)
AC_SUBST(V_MINOR)
AC_SUBST(DLL_PRELOAD)

AC_SUBST(XSANE_ICON)

AC_ARG_ENABLE(foo1, [
Used environment variables that can be set by user:
  CFLAGS, CPPFLAGS, LDFLAGS, LIBS])

AC_ARG_ENABLE(foo2, [
To add include or library paths call:
  [env] CPPFLAGS=\"-I/path/to/foo/include\" LDFLAGS=\"-L/path/to/foo/libs\" ./configure])

AC_OUTPUT([Makefile intl/Makefile po/Makefile lib/Makefile
	   src/Makefile include/Makefile doc/Makefile xsane.spec],)

echo "****************************************************************"
echo "*                                                              *"
echo "* XSANE configure status:                                      *"
echo "* -----------------------                                      *"

if test "${USE_NLS}" = "yes"; then
  if test "$USE_INCLUDED_LIBINTL" = yes; then
    echo "* - NLS activated (package internal)                           *"
  else
    echo "* - NLS activated (external)                                   *"
  fi
else
  echo "* - NLS deactivated                                            *"
fi

if test "${GIMP_LIBS}set" != "set"; then
  echo "* - GIMP plugin  activated                                     *"
else
  echo "* - GIMP plugin  deactivated                                   *"
fi

if test "${ac_cv_lib_jpeg_jpeg_start_decompress}" = "yes"; then
  echo "* - JPEG support activated                                     *"
else
  echo "* - JPEG support deactivated                                   *"
fi

if test "${ac_cv_lib_tiff_TIFFOpen}" = "yes"; then
  echo "* - TIFF support activated                                     *"
else
  echo "* - TIFF support deactivated                                   *"
fi

if test "${ac_cv_lib_png_png_create_info_struct}" = "yes"; then
  echo "* - PNG  support activated                                     *"
else
  echo "* - PNG  support deactivated                                   *"
fi

if test "${ac_cv_lib_lcms_cmsOpenProfileFromFile}" = "yes"; then
  echo "* - LCMS (color management) support activated                  *"
else
  echo "* - LCMS (color management) support deactivated                *"
fi

echo "*                                                              *"
echo "****************************************************************"
echo "*                                                              *"
echo "* To compile XSANE:                                            *"
echo "* -----------------                                            *"
echo "*   make                                                       *"
echo "*   make install                                               *"
echo "*                                                              *"
echo "* ------------------------------------------------------------ *"
echo "* ... PLEASE READ SANE DOCUMENTATION BEFORE STARTING XSANE ... *"
echo "* ------------------------------------------------------------ *"
echo "****************************************************************"
cat xsane.NEWS
