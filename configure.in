dnl Process this file with autoconf to produce a configure script. -*-sh-*-
AC_INIT(include/sane/config.h.in)
AC_CONFIG_HEADER(include/sane/config.h)
# AC_PREREQ(2.10)dnl		dnl Minimum Autoconf version required.
# AC_ARG_PROGRAM

# version code:
V_MAJOR=0
V_MINOR=33

PACKAGE=xsane
SANE_V_MAJOR=1
VERSION=${V_MAJOR}.${V_MINOR}
PACKAGE_VERSION="$PACKAGE-$VERSION"
AC_DEFINE_UNQUOTED(PACKAGE, "$PACKAGE")
AC_DEFINE_UNQUOTED(VERSION, "$VERSION")
AC_DEFINE_UNQUOTED(PACKAGE_VERSION, "$PACKAGE_VERSION")
AC_SUBST(PACKAGE)
AC_SUBST(VERSION)
AC_SUBST(PACKAGE_VERSION)
AC_SUBST(SANE_MAJOR)

dnl Checks for programs.
AC_PROG_CC
AC_AIX
AC_MINIX
AC_ISC_POSIX
AM_PROG_CC_STDC
AC_PROG_INSTALL
AC_PROG_MAKE_SET
AC_PROG_CPP
AC_PROG_GCC_TRADITIONAL

INCLUDES="${INCLUDES} -I/usr/local/include"
CPPFLAGS="${CPPFLAGS} -D_GNU_SOURCE"
if test "${ac_cv_prog_gcc}" = "yes"; then
  CFLAGS="${CFLAGS} -Wall"
fi

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(fcntl.h unistd.h libintl.h libc.h sys/dsreq.h sys/select.h \
    sys/time.h sys/scanio.h sys/socket.h sys/io.h asm/io.h gscdds.h sys/hw.h \
    bsd/dev/scsireg.h io/cam/cam.h camlib.h \
    sys/types.h zlib.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_C_INLINE
AC_TYPE_SIGNAL
AC_TYPE_SIZE_T
AC_TYPE_PID_T
AC_CHECK_TYPE(ssize_t, long)
AC_CHECK_TYPE(u_char, unsigned char)
AC_CHECK_TYPE(u_int, unsigned int)
AC_CHECK_TYPE(u_long, unsigned long)

dnl Checks for libraries.

AC_CHECK_LIB(scsi, scsireq_enter)       # FreeBSD needs this 
AC_CHECK_LIB(cam, cam_open_device)      # FreeBSD 3+ needs this
AC_CHECK_LIB(m,sqrt)
AC_CHECK_LIB(intl,gettext)
AC_CHECK_LIB(jpeg,jpeg_start_decompress)
AC_CHECK_LIB(z,deflateInit_)
AC_CHECK_LIB(tiff,TIFFOpen)

dnl Checks for library functions.
AM_FUNC_ALLOCA
AC_FUNC_MMAP
AC_CHECK_FUNCS(atexit mkdir sigprocmask strdup strndup strftime strstr \
    strsep strtod snprintf usleep strcasecmp strncasecmp)

dnl Checks for libraries.

#### Choose a window system.

AC_PATH_X
if test "$no_x" = yes; then
  window_system=none
else
  window_system=x11
fi

AC_CHECK_HEADERS(dlfcn.h,
 [AC_CHECK_LIB(dl,dlopen)
 AC_CHECK_FUNCS(dlopen, enable_dynamic=yes,)],)

# HP/UX DLL handling
AC_CHECK_HEADERS(dl.h,
 [AC_CHECK_LIB(dld,shl_load)
 AC_CHECK_FUNCS(shl_load, enable_dynamic=yes,)],)

AM_PATH_GTK(1.2.0, HAVE_GTK=yes, )

# Change CFLAGS temporarily so that C_SWITCH_X_SITE gets used
# for the tests that follow.  We set it back to REAL_CFLAGS later on.

if test "${HAVE_GTK}" = "no"; then
  echo "ERROR: gtk is needed for compilation"
  exit -1
fi

XSCAN="xsane"

# According to Owen Taylor, GTK_CFLAGS is _guaranteed_ to contain
# -D and -I flags only, i.e., it really is GTK_CPPFLAGS...
saved_CPPFLAGS="${CPPFLAGS}"
saved_LIBS="${LIBS}"
CPPFLAGS="${CPPFLAGS} ${GTK_CFLAGS}"
LIBS="${LIBS} ${GTK_LIBS}"

AC_CHECK_HEADERS(libgimp/gimp.h,GIMP_LIBS="-lgimp")

LIBS="${saved_LIBS}"

# png test must stand behind X11-check because it is located in
# the X11 directory on some systems
# I placed it here because the CPPFLAGS are saved here
AC_CHECK_HEADERS(png.h,
 [AC_CHECK_LIB(png, png_create_info_struct,,, -L${x_libraries})])
CPPFLAGS="${saved_CPPFLAGS}"

AC_SUBST(INCLUDES)
AC_SUBST(XSCAN)
AC_SUBST(GTK_CFLAGS)
AC_SUBST(GTK_LIBS)
AC_SUBST(GIMP_LIBS)
CPPFLAGS="${CPPFLAGS} \
	  -DPATH_SANE_DATA_DIR=\$(sanedatadir) \
	  -DV_MAJOR=${V_MAJOR} -DV_MINOR=${V_MINOR} -DSANE_V_MAJOR=${SANE_V_MAJOR}"

AC_CHECK_LIB(sane,sane_init,,, -L${x_libraries})
if test "${ac_cv_lib_sane_sane_init}" = "no"; then
  echo "ERROR: SANE is needed for compiling xsane"
  exit -1
fi

AC_SUBST(V_MAJOR)
AC_SUBST(V_MINOR)
AC_SUBST(DLL_PRELOAD)

AC_OUTPUT([Makefile lib/Makefile sanei/Makefile frontend/Makefile
	   include/Makefile doc/Makefile],)

echo "****************************************************************"
echo "* *** PLEASE READ SANE DOCUMENTATION BEFORE STARTING XSANE *** *"
echo "* ************************************************************ *"
echo "* to compile enter:                                            *"
echo "*   make                                                       *"
echo "* and as root:                                                 *"
echo "*   make install                                               *"
echo "****************************************************************"
